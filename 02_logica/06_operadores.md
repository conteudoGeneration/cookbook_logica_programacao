<h1>Operadores</h1>



Operadores s√£o s√≠mbolos que representam atribui√ß√µes, c√°lculos e ordem dos dados. Os Operadores s√£o elementos funcionais,  que atuam (Processamento) sobre os operandos (Entrada de dados), e produzem um determinado resultado (Sa√≠da de dados). Por exemplo, a express√£o 3 + 2 relaciona dois operandos (os n√∫meros 3 e 2) por meio do operador (+) que representa a opera√ß√£o de adi√ß√£o.

Assim como na matem√°tica, os operadores possuem uma ordem de prioridades (alguns c√°lculos s√£o processados antes de outros, por exemplo). 

Em rela√ß√£o ao tipo de dados, os Operadores s√£o classificados como:

- Operadores Aritm√©ticos;

- Operadores de Atribui√ß√£o;

- Operadores Relacionais.

- Operadores L√≥gicos.



<h2>1. Operadores Aritm√©ticos</h2>



Os **Operadores Aritm√©ticos** s√£o um conjunto de s√≠mbolos que representam as opera√ß√µes b√°sicas da matem√°tica (Soma, Subtra√ß√£o e etc). Esses operadores somente poder√£o ser utilizados entre vari√°veis com os tipos de dados Num√©ricos Inteiros e/ou Num√©ricos Reais.

| Operador | Opera√ß√£o        |
| -------- | --------------- |
| +        | Soma            |
| -        | Subtra√ß√£o       |
| *        | Multiplica√ß√£o   |
| /        | Divis√£o         |
| %        | M√≥dulo ou Resto |
| ++       | Incremento      |
| --       | Decremento      |
| - Numero | Numero Negativo |



Os Operadores Aritm√©ticos obedecem √†s regras matem√°ticas comuns:

- As express√µes de dentro de par√™nteses s√£o sempre resolvidas antes das express√µes fora dos par√™nteses;
- Quando existe um par√™ntese dentro de outro, a solu√ß√£o sempre inicia do par√™ntese mais interno at√© o mais externo (de dentro para fora);
- Quando duas ou mais express√µes tiverem a mesma prioridade, a solu√ß√£o √© sempre iniciada da express√£o mais √† esquerda at√© a mais √† direita.

| Operador |   Opera√ß√£o    | Prioridade |
| :------: | :-----------: | :--------: |
|    /     |    Divis√£o    |     2      |
|    *     | Multiplica√ß√£o |     2      |
|    +     |     Soma      |     3      |
|    -     |   Subtra√ß√£o   |     3      |



**Exemplo - Operadores Aritm√©ticos:**

```pseudocode
programa
{
	inclua biblioteca Matematica 
	
	funcao inicio()
	{
		inteiro x = 4
		inteiro y = 2
		inteiro z = 3
		inteiro w = -5
		
		escreva("Soma: " + (x + y))
		escreva("\nSoma com numero negativo: " + (x + w))
		escreva("\nSubtra√ß√£o: " + (x - y))
		escreva("\nMultiplica√ß√£o: " + (x * y))
		escreva("\nDivis√£o: " + (x / y))
		escreva("\nM√≥dulo (resto da divis√£o): " + (x % 2))
		escreva("\nM√≥dulo (resto da divis√£o): " + (z % 2))
		escreva("\nIncrementar: " + (z ++))
		escreva("\nDecrementar: " + (x --))
	}
}

```

 <br />

<div align="left"><img src="https://i.imgur.com/bQGvf3h.png" title="source: imgur.com" width="25px"/> <a href="https://github.com/rafaelq80/exemplos_logica/blob/main/operadores/operadores_matematicos.por" target="_blank"><b>C√≥digo fonte do exemplo</b></a></div>

<br />

<h3>1.1. Biblioteca Matematica</h3>

A Biblioteca Matematica oferece as fun√ß√µes Matem√°ticas mais comuns, que podem ser utilizadas no nosso Pseudoc√≥digo. Para usar esta Biblioteca √© necess√°rio declarar no inicio do programa atrav√©s do comando:

```pseudocode
inclua biblioteca Matematica 
```

<br />

| Fun√ß√£o                                                    | Opera√ß√£o                                                     |
| --------------------------------------------------------- | ------------------------------------------------------------ |
| Matematica.raiz(real valor, real indice)                  | Calcula a Raiz de um numero real (quadrada, c√∫bica, entre outras) |
| Matematica.potencia(real valor, real expoente)            | Calcula a Pot√™ncia de um numero real (ao quadrado, ao cubo, entre outras) |
| Matematica.valor_absoluto(real valor)                     | Retorna o valor absoluto de um numero real.                  |
| Matematica.arredondar(real valor, inteiro casas_decimais) | Arredonda o valor de um numero real. O segundo par√¢metro √© o numero de casas decimais. |
| Matematica.maior_numero(real valor_1, real valor_2)       | Indica entre 2 n√∫meros reais, qual deles √© o maior.          |
| Matematica.menor_numero(real valor_1, real valor_2)       | Indica entre 2 n√∫meros reais, qual deles √© o menor.          |


| <img src="https://i.imgur.com/RfjtOFi.png" title="source: imgur.com" width="120px"/> | <div align="left">**DICA:** *No Anexo Revis√£o de Matem√°tica, temos uma breve revis√£o sobre Potencia√ß√£o, Radicia√ß√£o e M√≥dulo. Caso voc√™ tenha alguma d√∫vida, n√£o deixe de ler!*</div> |
| ------------------------------------------------------------ | ------------------------------------------------------------ |



**Exemplo - Biblioteca Matematica:**

```pseudocode
programa
{
	inclua biblioteca Matematica 
	
	funcao inicio()
	{
		real x = 4.0
		real y = 7.75
		
		escreva("Radicia√ß√£o: " + Matematica.raiz(x, 2.0))
		escreva("\nPotencia√ß√£o: " + Matematica.potencia(x, 3.0))
		escreva("\nValor Absoluto: " + Matematica.valor_absoluto(x))
		escreva("\nValor Arredondado: " + Matematica.arredondar(y, 1))
		escreva("\nMaior valor entre 2 numeros: " + Matematica.maior_numero(x, y))
		escreva("\nMenor valor entre 2 numeros: " + Matematica.maior_numero(x, y))
		
	}
}

```

 <br />

<div align="left"><img src="https://i.imgur.com/bQGvf3h.png" title="source: imgur.com" width="25px"/> <a href="https://github.com/rafaelq80/exemplos_logica/blob/main/operadores/biblioteca_matematica.por" target="_blank"><b>C√≥digo fonte do exemplo</b></a></div>

<br />

<h2>2. Operadores de Atribui√ß√£o</h2>



Os **Operadores de Atribui√ß√£o** t√™m como fun√ß√£o retornar um valor atribu√≠do de acordo com a opera√ß√£o indicada. A opera√ß√£o √© feita entre os dois operandos, sendo atribu√≠do o resultado ao primeiro.

| Operador | Descri√ß√£o                    |
| -------- | ---------------------------- |
| =        | Atribui√ß√£o simples           |
| +=       | Atribui√ß√£o com soma          |
| -=       | Atribui√ß√£o com subtra√ß√£o     |
| *=       | Atribui√ß√£o com multiplica√ß√£o |
| /=       | Atribui√ß√£o com divis√£o       |
| %=       | Atribui√ß√£o com m√≥dulo        |



**Exemplo - Operadores de Atribui√ß√£o:**

```pseudocode
programa
{
	
	funcao inicio()
	{
		inteiro x = 4
		inteiro y = 2
		inteiro z = 1

		z = y
		escreva("Atribui√ß√£o simples (z = 2): " + z)

		x += y
		escreva("\nAtribui√ß√£o com soma (4 + 2): " + x)

		x -= z
		escreva("\nAtribui√ß√£o com subtra√ß√£o (6 - 2): " + x)

		x *= y
		escreva("\nAtribui√ß√£o com multiplica√ß√£o (4 * 2): " + x)

		x /= y
		escreva("\nAtribui√ß√£o com divis√£o (8 / 2): " + x)

	}
}
```

<br />

<div align="left"><img src="https://i.imgur.com/bQGvf3h.png" title="source: imgur.com" width="25px"/> <a href="https://github.com/rafaelq80/exemplos_logica/blob/main/operadores/operadores_atribuicao.por" target="_blank"><b>C√≥digo fonte do exemplo</b></a></div>

<br />

<h2>3. Operadores Relacionais</h2>



Os **Operadores Relacionais** s√£o utilizados para comparar valores entre vari√°veis e express√µes do mesmo tipo e criar declara√ß√µes condicionais. Esses operadores s√£o usados com o intuito de criar express√µes do tipo **verdadeiro** (TRUE) ou **falso** (FALSE), fundamentais para as declara√ß√µes condicionais. O **retorno da compara√ß√£o √© sempre um valor do tipo L√≥gico**.

<div align="center"><img src="https://i.imgur.com/SbtJ0Y0.png" title="source: imgur.com" width="25%""/></div>



| Operador | Descri√ß√£o              |
| -------- | ---------------------- |
| **>**    | Maior do que           |
| **>=**   | Maior do que ou  igual |
| **<**    | Menor do que           |
| **<=**   | Menor do que ou igual  |
| **==**   | Igual                  |
| **!=**   | Diferente              |


| <img src="https://i.imgur.com/hOgWvSc.png" title="source: imgur.com" width="100px"/> | **ATEN√á√ÉO:** N√£o confunda o Operador Relacional (==) com o Operador de Atribui√ß√£o (=).<br />O Operador de Atribui√ß√£o (=) tem por objetivo atribuir uma valor para uma vari√°vel.<br/>O Operador Relacional (==) tem por objetivo checar se os valores entre duas vari√°veis s√£o iguais. |
| ------------------------------------------------------------ | :----------------------------------------------------------- |



**Exemplo - Operadores Relacionais:**

```pseudocode
programa
{
	
	funcao inicio()
	{
		inteiro x = 10
		inteiro y = 5
		inteiro z = 20
		inteiro w = 5
		logico resposta

		resposta = x > y
		escreva("O valor de x √© maior do que o valor de y? " + resposta)
		
		resposta = z >= y
		escreva("\nO valor de z √© maior ou igual ao valor de y? " + resposta)

		resposta = x < z
		escreva("\nO valor de x √© menor do que o valor de z? " + resposta)
		
		resposta = z <= w
		escreva("\nO valor de z √© menor ou igual ao valor de w? " + resposta)

		resposta = x == z
		escreva("\nO valor de x √© igual ao valor de z? " + resposta)
		
		resposta = z != w
		escreva("\nO valor de z √© diferente do valor de w? " + resposta)
	}
}
```

<br />

<div align="left"><img src="https://i.imgur.com/bQGvf3h.png" title="source: imgur.com" width="25px"/> <a href="https://github.com/rafaelq80/exemplos_logica/blob/main/operadores/operadores_relacionais.por" target="_blank"><b>C√≥digo fonte do exemplo</b></a></div>

<br />

<h2>4. Operadores l√≥gicos</h2>



Os Operadores L√≥gicos s√£o utilizados para realizar compara√ß√µes e valida√ß√µes, criando express√µes condicionais complexas. 

Assim como podemos comparar objetos e lugares, dentro de um software tamb√©m podemos comparar dados. Podemos, por exemplo, verificar se o e-mail e a senha digitados s√£o iguais aos cadastrados no sistema, se a idade informada pelo usu√°rio √© maior ou igual a 18 anos, se um campo do formul√°rio foi preenchido ou est√° vazio, entre outras tantas compara√ß√µes que precisamos fazer para o bom funcionamento de uma aplica√ß√£o.

Esses operadores s√£o usados com o intuito de criar express√µes do tipo **verdadeiro** (TRUE) ou **falso** (FALSE), mas o seu funcionamento √© um pouquinho mais complexo.

<div align="center"><img src="https://i.imgur.com/SbtJ0Y0.png" title="source: imgur.com" width="25%""/></div>


| Opera√ß√£o L√≥gica | Operador | Descri√ß√£o                                                    |
| --------------- | :------: | ------------------------------------------------------------ |
| **Conjun√ß√£o**   |  **e**   | Valida se dois dados s√£o verdadeiros. √â escrito uma letra **e** entre duas proposi√ß√µes. |
| **Disjun√ß√£o**   |  **ou**  | V√°lida se um dos dois dados apresentados s√£o verdadeiros, ou se os dois s√£o verdadeiros. √â escrito com a palavra **ou** entre as duas proposi√ß√µes. |
| **Nega√ß√£o**     | **nao**  | Inverte o valor, o que √© verdadeiro passa a ser falso, o que √© falso passa a ser verdadeiro. √â a nega√ß√£o de uma senten√ßa. √â escrito com a palavra **nao** antes da proposi√ß√£o. |


Antes de aplicarmos os Operadores L√≥gicos no c√≥digo, vamos compreender como funcionam estes operadores. Para simplificar o nosso aprendizado vamos fazer o uso de **proposi√ß√µes**, **que s√£o frases e/ou senten√ßas que podem ser tomadas como verdadeiras ou falsas (premissas)**, para testar algumas valida√ß√µes e compara√ß√µes, e assim desenvolver ainda mais nossa consci√™ncia para um desenvolvimento de uma linha de racioc√≠nio que nos permite escrever c√≥digos mais objetivos para atingir as nossas metas.



<h3>4.1. Proposi√ß√µes simples</h3>



Vamos come√ßar com proposi√ß√µes simples:

Podemos afirmar que as seguintes frases ou senten√ßas s√£o proposi√ß√µes:

**A ü°™** *Sou participante do Bootcamp da Generation.*
**B ü°™** *O Vale Refei√ß√£o tem um valor di√°rio de R$ 30,00* 
**C ü°™** *Chove em Guaianazes.*

Todas as frases acima s√£o proposi√ß√µes, pois podemos validar se se trata de uma informa√ß√£o verdadeira ou falsa. Por
expressar um valor que pode ser confirmado ou negado.

**D ü°™** *Vai estudar hoje?*
**E ü°™** *Outro dia*
**F ü°™** *Aquelu participante*

As frases D, E e F n√£o s√£o proposi√ß√µes. Por qu√™? 

A frase **D** √© uma pergunta, logo n√£o pode ser validada por verdadeiro ou falso, para ser uma proposi√ß√£o precisa ser uma afirma√ß√£o ou nega√ß√£o.

As frases **E** e **F** n√£o indicam qual √© o dia e quem √© a pessoa participante, respectivamente, logo n√£o podemos validar como sendo uma senten√ßa verdadeira ou falsa, portanto n√£o √© uma proposi√ß√£o.



<h4>4.1.1. Nega√ß√£o da Proposi√ß√£o</h4>



Quando aplicamos a nega√ß√£o a uma proposi√ß√£o, ela deixa de ser verdadeira e se torna falsa ou deixa de ser falsa e se torna verdadeira. A nega√ß√£o, nega toda a ideia inicial dessa proposi√ß√£o.

**A ü°™** *Sou participante do Bootcamp da Generation.*

Essa proposi√ß√£o √© verdadeira, caso tenha passado pelo processo seletivo da Generation e feito sua inscri√ß√£o e seguido todas as orienta√ß√µes. Como ficaria se aplic√°ssemos o operador n√£o a essa proposi√ß√£o?

**nao A ü°™** *N√£o sou participante do Bootcamp da Generation.*

| <img src="https://i.imgur.com/hOgWvSc.png" title="source: imgur.com" width="100px"/> | <div align="left"> **ATEN√á√ÉO:** Observe que foi acrescentado antes da proposi√ß√£o o **nao (l√≥gico)**, para negar a proposi√ß√£o.</div> |
| ------------------------------------------------------------ | ------------------------------------------------------------ |

<br />

| <img src="https://i.imgur.com/hOgWvSc.png" title="source: imgur.com" width="100px"/> | <div align="left"> **ATEN√á√ÉO:** Na maioria das Linguagens de Programa√ß√£o a nega√ß√£o √© representada por: ! antes da vari√°vel.</div> |
| ------------------------------------------------------------ | ------------------------------------------------------------ |



<h3>4.2. Proposi√ß√µes compostas</h3>



<h4>4.2.1. Conjuntiva E</h4>

S√£o proposi√ß√µes formadas por duas ou mais senten√ßas que podem ser validadas como verdadeiras ou falsas. Para as proposi√ß√µes compostas conjuntivas utilizaremos o operador **e (l√≥gico)**:

**Exemplo:**

**G ü°™** *Chove e faz frio em SP.*


Na proposi√ß√£o G temos duas senten√ßas que podem ser validadas como verdadeiras ou falsas:

**Chove:** Podemos validar se √© verdadeiro ou falso observando o tempo e o clima atual.
**Faz frio em SP:** Podemos validar se √© verdadeiro ou falso observando o tempo e o clima atual.



| <img src="https://i.imgur.com/hOgWvSc.png" title="source: imgur.com" width="100px"/> | <div align="left"> **ATEN√á√ÉO:** Note que entre as duas senten√ßas temos um conector que √© o **E (l√≥gico)**.</div> |
| ------------------------------------------------------------ | ------------------------------------------------------------ |

<br />

| <img src="https://i.imgur.com/hOgWvSc.png" title="source: imgur.com" width="100px"/> | <div align="left"> **ATEN√á√ÉO:** Na maioria das Linguagens de Programa√ß√£o a conjun√ß√£o √© representada por: && entre as duas condi√ß√µes que ser√£o testadas.</div> |
| ------------------------------------------------------------ | ------------------------------------------------------------ |



<h4>4.2.2. Disjuntiva OU</h4>



Para as proposi√ß√µes compostas disjuntivas utilizaremos o operador **ou (l√≥gico)**:

**H ü°™** *Chego tarde em casa ou pego um √¥nibus cheio.*

**Chego tarde em casa:** Podemos avaliar se √© verdadeiro ou falso, eu chego tarde em casa √© uma proposi√ß√£o verdadeira ou falsa pra mim?
**Pego um √¥nibus cheio:** Podemos avaliar se √© verdadeiro ou falso, eu pego o √¥nibus cheio?



| <img src="https://i.imgur.com/hOgWvSc.png" title="source: imgur.com" width="100px"/> | <div align="left"> **ATEN√á√ÉO:** Note que entre as duas senten√ßas temos um conector que √© o **OU (l√≥gico)**.</div> |
| ------------------------------------------------------------ | ------------------------------------------------------------ |

<br />

| <img src="https://i.imgur.com/hOgWvSc.png" title="source: imgur.com" width="100px"/> | <div align="left"> **ATEN√á√ÉO:** Na maioria das Linguagens de Programa√ß√£o a disjun√ß√£o √© representada por: \|\| entre as duas condi√ß√µes que ser√£o testadas.</div> |
| ------------------------------------------------------------ | ------------------------------------------------------------ |

<br />

| <img src="https://i.imgur.com/RfjtOFi.png" title="source: imgur.com" width="120px"/> | <div align="left">**DICA:** *Lembre-se de revisar esse conte√∫do sempre que tiver d√∫vidas na leitura de uma condicional em seu algoritmo*</div> |
| ------------------------------------------------------------ | ------------------------------------------------------------ |

<br />

<h3>4.3. Tabela verdade</h3>



A **Tabela Verdade** √© um tipo de tabela matem√°tica, amplamente utilizada em l√≥gica, para determinar se uma f√≥rmula √© v√°lida. Ela nos ajuda a entender melhor como esses operadores podem agir em um algoritmo, pois eles apresentam um padr√£o de resposta, que nos permite j√° prever quais os poss√≠veis resultados ao utiliz√°-los.

**Orienta√ß√µes:**

1. Entenda A sendo uma proposi√ß√£o e !A a negativa de A
2. Entenda B sendo uma segunda proposi√ß√£o
3. Entenda V como verdadeiro
4. Entenda F como falso
5. Proposi√ß√£o A -> Toda pessoa desenvolvedora gosta de caf√©
6. proposi√ß√£o B -> Toda pessoa desenvolvedora Java gosta de chocolate

**Tabela Verdade da Nega√ß√£o**

|  A   |  !A  |
| :--: | :--: |
|  V   |  F   |
|  F   |  V   |


Aplicando a Tabela Verdade da Nega√ß√£o:

- Se proposi√ß√£o A √© verdade a sua negativa seria falsa, porque se toda pessoa desenvolvedora gosta de caf√©,
  logo nenhuma pessoa deveria n√£o gostar de caf√©!
- Se a proposi√ß√£o A √© falsa, logo entendemos que Alguma pessoa desenvolvedora n√£o gosta de caf√©, pois para
  ser negativa a proposi√ß√£o A, basta uma √∫nica pessoa desenvolvedora n√£o gostar do caf√©.

**Tabela Verdade da Conjun√ß√£o**

|  A   |  B  |  A e B  |
| :--: | :--: | :--: |
|  V   |  V   |  V  |
|  V   |  F   |  F  |
|  F   |  V   |  F  |
|  F   |  F   |  F  |

Aplicando a Tabela Verdade da Conjun√ß√£o:

- Se a proposi√ß√£o A √© verdadeira e que a proposi√ß√£o B tamb√©m seja verdadeira, o resultado da conjun√ß√£o ser√° verdadeiro. Se toda a pessoa desenvolvedora gosta de caf√© e toda pessoa desenvolvedora Java gosta de chocolate! N√£o h√° como alguma pessoa desenvolvedora n√£o gostar de caf√© e nem como alguma pessoa desenvolvedora n√£o gostar de chocolate.
- Se a proposi√ß√£o A √© verdadeira e a proposi√ß√£o B √© falsa, o resultado da conjun√ß√£o ser√° falso, pois uma conjun√ß√£o depende de duas verdades para ser verdadeira. 
- Se Toda pessoa desenvolvedora gosta de caf√© e alguma pessoa desenvolvedora Java n√£o gosta de chocolate, assumindo que a proposi√ß√£o A √© falsa e assumindo que a proposi√ß√£o B tamb√©m seja falsa, o resultado da conjun√ß√£o ser√° falso, pois uma conjun√ß√£o depende de duas verdades para ser verdadeira.
- Se Alguma pessoa desenvolvedora n√£o gosta de caf√© e Alguma pessoa desenvolvedora Java n√£o gosta de
  chocolate a conjun√ß√£o ser√° falsa, pois uma conjun√ß√£o depende de duas verdades para ser verdadeira.



**Tabela Verdade da Disjun√ß√£o**

|  A   |  B  |  A e B  |
| :--: | :--: | :--: |
|  V   |  V   |  V  |
|  V   |  F   |  V  |
|  F   |  V   |  V  |
|  F   |  F   |  F  |

Aplicando a Tabela Verdade da Disjun√ß√£o:

- Se a proposi√ß√£o A √© verdadeira e assumindo que a proposi√ß√£o B tamb√©m √© verdadeira, o resultado da disjuntiva ser√° verdadeiro. Para uma condi√ß√£o verdadeira da disjun√ß√£o precisamos que uma de suas proposi√ß√µes seja verdadeira, sendo as duas verdadeiras temos um resultado verdadeiro.

- Se a proposi√ß√£o A √© verdade e assumindo que a proposi√ß√£o B √© falsa, o resultado da disjuntiva ser√° verdadeiro. Mesmo que uma das proposi√ß√µes seja falsa para a disjuntiva OU temos um resultado verdadeiro. OU (A) √© verdadeiro OU (B) √© verdadeiro.
- Se a proposi√ß√£o A √© falsa e assumindo que a proposi√ß√£o B √© falsa, o resultado da disjuntiva ser√° falso. Por n√£o ter nenhuma proposi√ß√£o que valide a opera√ß√£o, podemos dizer que esta se torna falsa.

<br />

**Exemplos - Operadores L√≥gicos:**

1) **Nega√ß√£o**

```pseudocode
programa
{
	
	funcao inicio()
	{
		inteiro x = 10
		inteiro y = 5
		logico proposicao_01
		logico proposicao_02
		logico resposta

		proposicao_01 = x > y
		escreva("\nA proposicao_01 √©: " + proposicao_01)
		
		proposicao_02 = x < y
		escreva("\nA proposicao_02 √©: " + proposicao_02)

		escreva("\n\nNega√ß√£o\n")

		resposta = nao proposicao_01
		escreva("\nA Nega√ß√£o da proposicao_01 √©: " + resposta)

		resposta = nao proposicao_02
		escreva("\nA Nega√ß√£o da proposicao_02 √©: " + resposta)
		
	}
}

```

<br />

<div align="left"><img src="https://i.imgur.com/bQGvf3h.png" title="source: imgur.com" width="25px"/> <a href="https://github.com/rafaelq80/exemplos_logica/blob/main/operadores/operadores_logicos_negacao.por" target="_blank"><b>C√≥digo fonte do exemplo</b></a></div>

<br />

2) **Conjun√ß√£o**

```pseudocode
programa
{
	
	funcao inicio()
	{
		inteiro x = 10
		inteiro y = 5
		logico proposicao_01
		logico proposicao_02
		logico proposicao_03
		logico proposicao_04
		logico resposta

		proposicao_01 = x > y
		escreva("\nA proposicao_01 √©: " + proposicao_01)
		
		proposicao_02 = x < y
		escreva("\nA proposicao_02 √©: " + proposicao_02)

		proposicao_03 = x != y
		escreva("\nA proposicao_03 √©: " + proposicao_03)
		
		proposicao_04 = x == y
		escreva("\nA proposicao_04 √©: " + proposicao_04)

		escreva("\n\nConjun√ß√£o\n")

		resposta = proposicao_01 e proposicao_03
		escreva("\nA proposicao_01 e proposicao_03 s√£o verdadeiras? " + resposta)

		resposta = proposicao_02 e proposicao_04
		escreva("\nA proposicao_02 e proposicao_04 s√£o verdadeiras? " + resposta)

		resposta = proposicao_01 e proposicao_02
		escreva("\nA proposicao_01 e proposicao_02 s√£o verdadeiras? " + resposta)

		resposta = proposicao_03 e proposicao_04
		escreva("\nA proposicao_03 e proposicao_04 s√£o verdadeiras? " + resposta)
		
	}
}

```

<br />

<div align="left"><img src="https://i.imgur.com/bQGvf3h.png" title="source: imgur.com" width="25px"/> <a href="https://github.com/rafaelq80/exemplos_logica/blob/main/operadores/operadores_logicos_conjuncao.por" target="_blank"><b>C√≥digo fonte do exemplo</b></a></div>

<br />

3) **Disjun√ß√£o**

```pseudocode
programa
{
	
	funcao inicio()
	{
		inteiro x = 10
		inteiro y = 5
		logico proposicao_01
		logico proposicao_02
		logico proposicao_03
		logico proposicao_04
		logico resposta

		proposicao_01 = x > y
		escreva("\nA proposicao_01 √©: " + proposicao_01)
		
		proposicao_02 = x < y
		escreva("\nA proposicao_02 √©: " + proposicao_02)

		proposicao_03 = x != y
		escreva("\nA proposicao_03 √©: " + proposicao_03)
		
		proposicao_04 = x == y
		escreva("\nA proposicao_04 √©: " + proposicao_04)

		escreva("\n\nDisjun√ß√£o\n")

		resposta = proposicao_01 ou proposicao_03
		escreva("\nA proposicao_01 e proposicao_03 s√£o verdadeiras? " + resposta)

		resposta = proposicao_01 ou proposicao_02
		escreva("\nA proposicao_01 e proposicao_02 s√£o verdadeiras? " + resposta)

		resposta = proposicao_03 ou proposicao_04
		escreva("\nA proposicao_03 e proposicao_04 s√£o verdadeiras? " + resposta)

		resposta = proposicao_02 ou proposicao_04
		escreva("\nA proposicao_02 e proposicao_04 s√£o verdadeiras? " + resposta)
	}
}

```

<br />

<div align="left"><img src="https://i.imgur.com/bQGvf3h.png" title="source: imgur.com" width="25px"/> <a href="https://github.com/rafaelq80/exemplos_logica/blob/main/operadores/operadores_logicos_disjuncao.por" target="_blank"><b>C√≥digo fonte do exemplo</b></a></div>

<br /><br />

<div align="left"><a href="README.md"><img src="https://i.imgur.com/XMgF3gl.png" title="source: imgur.com" width="3%"/>Voltar</a></div>
